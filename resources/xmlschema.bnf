(*
  A pretty complete BNF definitation of xml-schema according to 
  https://www.w3schools.com/xml/schema_elements_ref.asp
*)

schema = eb 'schema' attrs-with-sp 
		 ((include|import|redefine|annotation)*,(((simpleType|complexType|
		 group|attributeGroup)|element|attribute|notation),annotation*)*)
		 ee
include = eb 'include' attrs-with-sp annotation? ee  
import = eb 'import' attrs-with-sp annotation? ee 
redefine = eb 'redefine' attrs-with-sp (annotation?|(simpleType|complexType|group|attributeGroup))* ee 	   
notation = eb 'notation' attrs-with-sp annotation? ee 	   
choice = eb 'choice' attrs-with-sp (annotation?,(element|group|choice|sequence|any)*) ee
element = eb 'element' attrs-with-sp annotation?,(simpleType|complexType)?,(unique|key|keyref)* ee 
unique = eb 'unique' attrs-with-sp (annotation?,(selector,field+)) ee 
key = eb 'key' attrs-with-sp (annotation?,(selector,field+)) ee
selector = eb 'selector' attrs-with-sp annotation? ee
field = eb 'field' attrs-with-sp annotation? ee
keyref = eb 'keyref' attrs-with-sp (annotation?,(selector,field+)) ee 
complexType = eb 'complexType' attrs-with-sp 
	(annotation?,(simpleContent|complexContent|((group|all|
	 choice|sequence)?,((attribute|attributeGroup)*,anyAttribute?))))
	 ee
group = eb 'group' attrs-with-sp (annotation?,(all|choice|sequence)?) ee
any = eb 'any' attrs-with-sp annotation? ee 
simpleType = eb 'simpleType' attrs-with-sp (annotation? (simpleType-restriction| list| union)) ee
anyAttribute = eb 'anyAttribute' attrs-with-sp annotation? ee 
attributeGroup = eb 'attributeGroup' attrs-with-sp annotation? (attribute|attributeGroup)* anyAttribute? ee
complexContent = eb 'complexContent' attrs-with-sp (annotation?,(complexContent-restriction|extension)) ee
simpleContent = eb 'simpleContent' attrs-with-sp (annotation?,(extension | simpleType-restriction)) ee
extension = eb 'extension' attrs-with-sp (annotation?,((group|all|choice|sequence)?, ((attribute|attributeGroup)*,anyAttribute?))) ee 
all = eb 'all' attrs-with-sp annotation? element* ee
sequence = eb 'sequence' attrs-with-sp annotation?,(element|group|choice|sequence|any)* ee
union = eb 'union' attrs-with-sp (annotation?,(simpleType*)) ee 	   
list = eb 'list' attrs-with-sp  (annotation?,(simpleType?)) ee 	   
simpleType-restriction = eb <'restriction'> attrs-with-sp 
                         (annotation?,(simpleType?,(minExclusive|minInclusive|
						  maxExclusive|maxInclusive|totalDigits|fractionDigits|
						  length|minLength|maxLength|enumeration|whiteSpace|pattern)*)) 
						  ee
simpleContent-restriction = eb <'restriction'> attrs-with-sp 
						(annotation?,(simpleType?,(minExclusive |minInclusive|
						maxExclusive|maxInclusive|totalDigits|fractionDigits|
						length|minLength|maxLength|enumeration|whiteSpace|pattern)*)?,
						((attribute|attributeGroup)*,anyAttribute?))
                        ee
complexContent-restriction = eb <'restriction'> attrs-with-sp
							 (annotation?,(group|all|choice|sequence)?,
							 ((attribute|attributeGroup)*,anyAttribute?))
							 ee                         
attribute = eb 'attribute' attrs-with-sp annotation? simpleType? ee
fractionDigits = eb <'fractionDigits'> attrs-with-sp annotation? ee 
totalDigits = eb <'totalDigits'> attrs-with-sp annotation? ee 
whiteSpace = eb <'whiteSpace'> attrs-with-sp annotation? ee 
minInclusive = eb <'minInclusive'> attrs-with-sp annotation? ee
maxInclusive = eb <'maxInclusive'> attrs-with-sp annotation? ee
minExclusive = eb <'minExclusive'> attrs-with-sp annotation? ee
maxExclusive = eb <'maxExclusive'> attrs-with-sp annotation? ee
enumeration = eb <'enumeration'> attrs-with-sp annotation? ee
length = eb <'length'> attrs-with-sp annotation? ee
minLength = eb <'minLength'> attrs-with-sp annotation? ee
maxLength = eb <'maxLength'> attrs-with-sp annotation? ee
pattern = eb <'pattern'> attrs-with-sp annotation? ee
app-info = eb 'appinfo' attrs-with-sp string ee 
documentation = eb 'documentation' attrs-with-sp string ee  
annotation = eb 'annotation' attrs-with-sp (app-info | documentation)* ee
decimalLit = ('+' | '-')? decimalDigit*
decimalDigit = #'[0-9]'
string = charValue*
charValue = #"[^\n]"
<space> = <#'[ \t\n,]+'>
os = <#'[ \t\n]*'>
<attrs-with-sp> = (space attrs)?
attrs = ab (ident space string-literal)? (',' os ident space string-literal)* ae
<eb> = < os '['> ns? <':'>
<ee> = <']'> 
<ab> = < '{'>
<ae> = <'}'>
ns = ident
<letter> 	 = #'[A-Za-z]'
decimalDigit = #'[0-9]'
octalDigit   = #'[0-7]'
hexDigit     = #'[0-9a-fA-F]'
string-literal = '\"' (letter | decimalDigit | #'[-_/.:]')* '\"'
ns-ident = ident? ident
ident = ':' (letter { letter | decimalDigit | "_" })*